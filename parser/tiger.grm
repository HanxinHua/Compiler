structure A = Absyn

(*
Possibly the biggest hack I've ever written :))
Using instanceof in a super polymorph language is bad,
but so is sleep deprivation so ¯\_(ツ)_/¯
*)
datatype wrapper = Sym of Symbol.symbol * int
                  | Express of A.exp * int

fun packl (nil, parent) = parent
  | packl (a::l, parent) =
  case a of Sym(a) => packl(l, A.FieldVar(parent, (#1 a), (#2 a)))
  | Express(a) => packl(l, A.SubscriptVar(parent, (#1 a), (#2 a)))
%%
%term
    EOF
  | ID of string
  | INT of int | STRING of string
  | COMMA | COLON | SEMICOLON | LPAREN | RPAREN | LBRACK | RBRACK
  | LBRACE | RBRACE | DOT
  | PLUS | MINUS | TIMES | DIVIDE | EQ | NEQ | LT | LE | GT | GE | UMINUS
  | AND | OR | ASSIGN
  | ARRAY | IF | THEN | ELSE | WHILE | FOR | TO | DO | LET | IN | END | OF
  | BREAK | NIL
  | FUNCTION | VAR | TYPE
  | ERROR | LOWPRIORITY

%nonterm
		exp of A.exp | program of A.exp | lvalue of A.var | expseq of (A.exp * pos) list
  | explist of A.exp list | field of (Symbol.symbol * A.exp * pos) list | declarationlist of A.dec list
	| declaration of A.dec | typedeclaration of {name: Symbol.symbol, pos: pos, ty: A.ty}
  | functiondeclaration of A.fundec | typefield of A.field | typefields of A.field list
  | tyDecs of {name: Symbol.symbol, pos: pos, ty: A.ty} list
  |funDecs of A.fundec list | vardec of A.dec | ty of A.ty | lvaluebody of wrapper list

%pos int
%verbose
%start program
%eop EOF
%noshift EOF

%name Tiger

%keyword
WHILE FOR TO BREAK LET IN END FUNCTION VAR TYPE ARRAY IF THEN ELSE DO OF NIL

%prefer THEN ELSE LPAREN

%nonassoc LOWPRIORITY
%nonassoc ASSIGN VAR FUNCTION TYPE THEN DO OF
%nonassoc ELSE
%left OR
%left AND
%nonassoc EQ NEQ LT LE GT GE
%left PLUS MINUS
%left DIVIDE TIMES
%left UMINUS


%change    EQ -> ASSIGN | ASSIGN -> EQ
		|  SEMICOLON ELSE -> ELSE |  ->IN INT END

%%

program	: exp				                    (exp)



exp:   STRING                           (A.StringExp(STRING,STRINGleft))
	|  INT                                (A.IntExp(INT))
	|  NIL                                (A.NilExp)
	|  lvalue                             (A.VarExp lvalue)
	|  MINUS exp  %prec UMINUS            (A.OpExp{left = A.IntExp(0), oper = A.MinusOp,  right = exp,          pos = MINUSleft})
	|  exp PLUS exp                       (A.OpExp{left = exp1, oper = A.PlusOp,   right = exp2,                pos = PLUSleft})
	|  exp MINUS exp                      (A.OpExp{left = exp1, oper = A.MinusOp,  right = exp2,                pos = MINUSleft})
	|  exp TIMES exp                      (A.OpExp{left = exp1, oper = A.TimesOp,  right = exp2,                pos = TIMESleft})
	|  exp DIVIDE exp                     (A.OpExp{left = exp1, oper = A.DivideOp, right = exp2,                pos = DIVIDEleft})
	|  exp AND exp                        (A.IfExp{test = exp1, then' = exp2,      else' = SOME(A.IntExp(0)),   pos = ANDleft})
	|  exp OR exp                         (A.IfExp{test = exp1, then' =A.IntExp(1),else' = SOME exp2,           pos = ORleft})
	|  exp EQ exp                         (A.OpExp{left = exp1, oper = A.EqOp,     right = exp2,                pos = EQleft})
	|  exp NEQ exp                        (A.OpExp{left = exp1, oper = A.NeqOp,    right = exp2,                pos = NEQleft})
	|  exp LT exp                         (A.OpExp{left = exp1, oper = A.LtOp,     right = exp2,                pos = LTleft})
	|  exp LE exp                         (A.OpExp{left = exp1, oper = A.LeOp,     right = exp2,                pos = LEleft})
	|  exp GT exp                         (A.OpExp{left = exp1, oper = A.GtOp,     right = exp2,                pos = GTleft})
	|  exp GE exp                         (A.OpExp{left = exp1, oper = A.GeOp,     right = exp2,                pos = GEleft})
	|  lvalue ASSIGN exp                  (A.AssignExp{var = lvalue,               exp = exp,                   pos = ASSIGNleft})
	|  ID LPAREN explist RPAREN           (A.CallExp{func = Symbol.symbol ID,      args = rev explist,              pos = IDleft})
    |  ID LPAREN RPAREN                   (A.CallExp{func = Symbol.symbol ID,      args = [],                        pos = IDleft})
	|  LPAREN expseq RPAREN               (A.SeqExp( rev expseq))
	|  ID LBRACE field RBRACE             (A.RecordExp{fields = rev field, typ = Symbol.symbol ID,                  pos=IDleft})
	|  ID LBRACK exp RBRACK OF exp        (A.ArrayExp{typ = Symbol.symbol ID, size = exp1, init = exp2,              pos = IDleft})
	|  IF exp THEN exp                    (A.IfExp{test = exp1, then' = exp2,      else' = NONE,                pos = IFleft})
	|  IF exp THEN exp ELSE exp           (A.IfExp{test = exp1, then' = exp2,      else' = SOME exp3,           pos = IFleft})
	|  WHILE exp DO exp                   (A.WhileExp{test = exp1,  body = exp2,                                pos = WHILEleft})
	|  FOR ID ASSIGN exp TO exp DO exp    (A.ForExp{var = Symbol.symbol ID, escape = ref false, lo = exp1, hi = exp2, body = exp3, pos = FORleft})
	|  BREAK                              (A.BreakExp(BREAKleft))
	|  LET declarationlist IN expseq END  (A.LetExp{decs = (rev declarationlist), body = A.SeqExp (rev expseq),                      pos = LETleft})

lvalue: ID lvaluebody            (packl(lvaluebody, A.SimpleVar(Symbol.symbol ID, IDleft)))


lvaluebody: DOT ID lvaluebody            (Sym(Symbol.symbol ID, DOTleft) ::lvaluebody)
          | LBRACK exp RBRACK lvaluebody (Express(exp, LBRACKleft) :: lvaluebody)
          | (*derive empty*)             (nil)


expseq:   exp                 ((exp, expleft) :: [])
	  | expseq SEMICOLON exp  ((exp, expleft) :: expseq)
	  |                       ([])


explist:  exp           (exp :: [])
		| explist COMMA exp (exp :: explist)

declarationlist:   declarationlist declaration (declaration :: declarationlist)
                 | (*Derive empty*) ([])

declaration: tyDecs %prec LOWPRIORITY (A.TypeDec(rev tyDecs))
			| vardec       (vardec)
			| funDecs %prec LOWPRIORITY     (A.FunctionDec(rev funDecs))

vardec: VAR ID ASSIGN exp (A.VarDec{name=Symbol.symbol ID, escape=ref false,
                                        typ = Option.NONE, init=exp, pos=VARleft})
					 | VAR ID COLON ID ASSIGN exp (A.VarDec{name=Symbol.symbol ID1, escape=ref false,
                                        typ = Option.SOME((Symbol.symbol ID2, ID2left)), init=exp, pos=VARleft})

functiondeclaration: FUNCTION ID LPAREN typefields RPAREN EQ exp ({name = Symbol.symbol ID, params = (rev typefields),
															result = NONE,
															body = exp,
															pos = FUNCTIONleft}: A.fundec)
                     | FUNCTION ID LPAREN  RPAREN EQ exp ({name = Symbol.symbol ID, params = [],
															result = NONE,
															body = exp,
															pos = FUNCTIONleft}: A.fundec)
                     | FUNCTION ID LPAREN typefields RPAREN COLON ID EQ exp ({name = Symbol.symbol ID1, params = (rev typefields),
															result = SOME(Symbol.symbol ID2, ID2left),
															body = exp,
															pos = FUNCTIONleft}: A.fundec)
                     | FUNCTION ID LPAREN RPAREN COLON ID EQ exp ({name = Symbol.symbol ID1, params = [],
															result = SOME(Symbol.symbol ID2, ID2left),
															body = exp,
															pos = FUNCTIONleft}: A.fundec)

funDecs: functiondeclaration (functiondeclaration :: [])
       | funDecs functiondeclaration (functiondeclaration :: funDecs)

field: ID EQ exp             ((Symbol.symbol ID, exp, IDleft) :: [])
	   | field COMMA ID EQ exp ((Symbol.symbol ID, exp, IDleft) :: field)

typedeclaration: TYPE ID EQ ty ({name=Symbol.symbol ID, ty=ty, pos=IDleft})

tyDecs: typedeclaration        (typedeclaration :: [])
      | tyDecs typedeclaration (typedeclaration :: tyDecs)

ty: ID                          (A.NameTy(Symbol.symbol ID, IDleft))
    | LBRACE typefields RBRACE  (A.RecordTy(rev typefields))
    | LBRACE RBRACE             (A.RecordTy([]))
    | ARRAY OF ID               (A.ArrayTy(Symbol.symbol ID, ARRAYleft))

typefields: typefield                   (typefield :: [])
           | typefields COMMA typefield (typefield :: typefields)

typefield: ID COLON ID ({name = Symbol.symbol ID1, escape = ref false, typ = Symbol.symbol ID2, pos = ID1left} : A.field)
